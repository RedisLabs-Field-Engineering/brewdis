buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$bootPluginVersion"
        classpath "com.github.node-gradle:gradle-node-plugin:$nodePluginVersion"
    }
}

apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'brewdis-kafka'

projects {
    directories = ['app']

    plugins {
        all {
            id 'idea'
        }
        path(':') {
            id 'org.kordamp.gradle.java-project'
        }
        path(':brewdis-ui') {
            id 'java-library'
            id 'com.github.node-gradle.node'
        }
        path(':brewdis-api') {
            id 'java-library'
            id 'org.springframework.boot'
            id 'io.spring.dependency-management'
        }
    }
}

enforce {
    mergeStrategy = 'append'

    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "com.redis:lettucemod:$lettucemodVersion",
                "com.google.guava:guava:30.0-android",
                "com.google.errorprone:error_prone_annotations:2.4.0",
                "com.google.http-client:google-http-client:$googleHttpVersion",
                "com.google.http-client:google-http-client-jackson2:$googleHttpVersion",
                "com.google.api.grpc:proto-google-common-protos:2.0.1",
                "com.google.api:gax:1.60.1",
                "io.grpc:grpc-context:1.34.0",
                "com.google.protobuf:protobuf-java:$protobufVersion",
                "com.google.protobuf:protobuf-java-util:$protobufVersion",
                "org.hdrhistogram:HdrHistogram:2.1.12",
                "com.google.code.findbugs:jsr305:3.0.2"
    }
}
